import Quickshell
import Quickshell.Wayland
import Quickshell.Io
import QtQuick
import ".."

PanelWindow {
    id: clockPanel
    required property ShellScreen targetScreen
    screen: targetScreen

    UiPerformance { id: uiPerf }

        // ┌─────────────────────────────────────┐
        // │           Widget position           │
        // ├─────────────────────────────────────┤
        // │  active side (true/false)           │
            anchors.top: true                  
            anchors.right: true                
            anchors.left: true                 
            anchors.bottom: true               
        //  Position     
            margins.top: 0                   
            margins.right: 0                    
            margins.left: 0                   
            margins.bottom: 0                   
        // └─────────────────────────────────────┘

        WlrLayershell.layer: WlrLayer.Bottom
        WlrLayershell.namespace: "clock-widget"
        WlrLayershell.exclusiveZone: -1
        color: "transparent"

        // Auto-contrast: white by default, switch to black on bright wallpapers.
        property color clockTextColor: "#ffffff"
        Behavior on clockTextColor {
            ColorAnimation {
                duration: uiPerf.ms(220)
                easing.type: Easing.OutCubic
            }
        }
        property string wallpaperPath: ""
        readonly property real brightThreshold: 0.5
        property var wallpaperPositions: ({})
        property bool useCustomPosition: false
        property bool centerOnScreen: false
        property bool centerX: false
        property bool centerY: false
        property real customPosX: 0
        property real customPosY: 0
        property real dayFontSize: 90
        property real dateFontSize: 20
        property real timeFontSize: 17
        property string forcedTextColor: ""
        property int moveAnimMs: uiPerf.ms(320)
        property string pendingWeId: ""
        property string pendingWeProjectRaw: ""
        readonly property bool isVertical: targetScreen.height > targetScreen.width
        readonly property string positionsFilePath: Quickshell.env("HOME") + "/.config/quickshell/components/ModernClockWidget/" + (isVertical ? "positionsvertical.json" : "positions.json")
        readonly property string skwdStateFilePath: Quickshell.env("HOME") + "/.cache/skwd-wall/last-wallpaper-" + targetScreen.name + ".json"

        function shellQuote(s) {
            return "'" + String(s).replace(/'/g, "'\"'\"'") + "'"
        }

        function isStaticImage(path) {
            var p = (path || "").toLowerCase()
            return p.endsWith(".png") || p.endsWith(".jpg") || p.endsWith(".jpeg") || p.endsWith(".webp")
        }

        function resolveCurrentWallpaper() {
            var raw = stateFile.text().trim()
            if (raw) {
                try {
                    var state = JSON.parse(raw)
                    if (state.type === "static" && state.path) {
                        updateContrastFromPath(state.path)
                        return
                    }
                    if (state.type === "video" && state.path) {
                        // Videos do not have stable brightness sampling from source.
                        // Keep color/position in sync using the selected file path.
                        updateContrastFromPath(state.path)
                        return
                    }
                    if (state.type === "we" && state.we_id) {
                        pendingWeId = String(state.we_id)
                        pendingWeProjectRaw = ""
                        resolveWeProjectProc.command = [
                            "cat",
                            Quickshell.env("HOME") + "/.local/share/Steam/steamapps/workshop/content/431960/" + pendingWeId + "/project.json"
                        ]
                        resolveWeProjectProc.running = false
                        resolveWeProjectProc.running = true
                        return
                    }
                } catch (e) {
                }
            }
            resolveWallpaperProc.running = false
            resolveWallpaperProc.running = true
        }

        function basename(path) {
            var p = String(path || "")
            var parts = p.split("/")
            return parts.length > 0 ? parts[parts.length - 1] : ""
        }

        function positiveNumberOr(value, fallback) {
            var n = Number(value)
            return (isFinite(n) && n > 0) ? n : fallback
        }

        function normalizeTextColor(value) {
            var c = String(value || "").trim().toLowerCase()
            if (c === "white" || c === "#ffffff") return "#ffffff"
            if (c === "black" || c === "#000000") return "#000000"
            return ""
        }

        function applyPositionFromPath(path) {
            var full = String(path || "")
            var file = basename(full)
            // Use exact filename match (with extension) from positions.json
            var cfg = wallpaperPositions[file] || wallpaperPositions.default || null
            var matchedKey = wallpaperPositions[file] ? file : "default"
            
            var defaultCfg = wallpaperPositions.default && typeof wallpaperPositions.default === "object"
                ? wallpaperPositions.default
                : ({})

            // Typography controls (global in default + per-wall override)
            dayFontSize = positiveNumberOr(cfg && cfg.daySize, positiveNumberOr(defaultCfg.daySize, 90))
            dateFontSize = positiveNumberOr(cfg && cfg.dateSize, positiveNumberOr(defaultCfg.dateSize, 20))
            timeFontSize = positiveNumberOr(cfg && cfg.timeSize, positiveNumberOr(defaultCfg.timeSize, 17))
            forcedTextColor = normalizeTextColor((cfg && cfg.textColor) || defaultCfg.textColor)

            // Position mode: exact center when centerOnScreen=true, otherwise use x/y.
            centerOnScreen = (cfg && cfg.centerOnScreen !== undefined)
                ? cfg.centerOnScreen === true
                : defaultCfg.centerOnScreen === true

            // Axis-specific centering; centerOnScreen forces both axes centered.
            centerX = centerOnScreen || ((cfg && cfg.centerX !== undefined)
                ? cfg.centerX === true
                : defaultCfg.centerX === true)
            centerY = centerOnScreen || ((cfg && cfg.centerY !== undefined)
                ? cfg.centerY === true
                : defaultCfg.centerY === true)
            console.log("Clock position resolve:", full, "->", matchedKey,
                        "centerOnScreen=", centerOnScreen, "centerX=", centerX, "centerY=", centerY)

            if (!cfg || typeof cfg !== "object") {
                customPosX = 0
                customPosY = 0
                useCustomPosition = true
                return
            }

            var x = Number(cfg.x)
            var y = Number(cfg.y)
            // Apply position if specified in cfg, otherwise default to 0.
            customPosX = isFinite(x) ? x : 0
            customPosY = isFinite(y) ? y : 0
            useCustomPosition = true

            console.log("Clock position final:", matchedKey,
                        "x=", customPosX, "y=", customPosY)
        }

        function updateContrastFromPath(path) {
            wallpaperPath = path || ""
            applyPositionFromPath(wallpaperPath)

            if (forcedTextColor !== "") {
                clockTextColor = forcedTextColor
                return
            }

            if (!isStaticImage(wallpaperPath)) {
                clockTextColor = "#ffffff"
                return
            }

            measureBrightnessProc.command = [
                "bash",
                "-lc",
                "magick identify -format '%[fx:mean]' " + shellQuote(wallpaperPath) + " 2>/dev/null"
            ]
            measureBrightnessProc.running = false
            measureBrightnessProc.running = true
        }

        FileView {
            id: positionsFile
            path: clockPanel.positionsFilePath
            watchChanges: true
            onLoaded: {
                var raw = positionsFile.text().trim()
                if (!raw) {
                    clockPanel.wallpaperPositions = ({})
                    return
                }
                try {
                    var parsed = JSON.parse(raw)
                    clockPanel.wallpaperPositions = parsed && typeof parsed === "object" ? parsed : ({})
                } catch (e) {
                    clockPanel.wallpaperPositions = ({})
                }
                clockPanel.resolveCurrentWallpaper()
            }
            onFileChanged: positionsFile.reload()
        }

        FileView {
            id: stateFile
            path: clockPanel.skwdStateFilePath
            watchChanges: true
            onLoaded: clockPanel.resolveCurrentWallpaper()
            onFileChanged: stateFile.reload()
        }

        Process {
            id: resolveWallpaperProc
            command: ["readlink", "-f", Quickshell.env("HOME") + "/.config/hypr/wallpaper_effects/.wallpaper_current"]

            stdout: StdioCollector {
                waitForEnd: true
                onStreamFinished: {
                    clockPanel.updateContrastFromPath(text.trim())
                }
            }

            onExited: code => {
                if (code !== 0) {
                    clockPanel.clockTextColor = "#ffffff"
                }
            }
        }

        Process {
            id: resolveWeProjectProc
            command: ["true"]

            stdout: StdioCollector {
                waitForEnd: true
                onStreamFinished: {
                    clockPanel.pendingWeProjectRaw = text.trim()
                }
            }

            onExited: {
                var key = clockPanel.pendingWeId
                var raw = clockPanel.pendingWeProjectRaw
                if (raw) {
                    try {
                        var p = JSON.parse(raw)
                        if (p && p.title)
                            key = String(p.title)
                    } catch (e) {
                    }
                }
                clockPanel.updateContrastFromPath(key)
            }
        }

        Process {
            id: measureBrightnessProc
            command: ["true"]

            stdout: StdioCollector {
                waitForEnd: true
                onStreamFinished: {
                    var value = parseFloat(text.trim())
                    if (isNaN(value)) {
                        clockPanel.clockTextColor = "#ffffff"
                        return
                    }
                    clockPanel.clockTextColor = value >= clockPanel.brightThreshold ? "#000000" : "#ffffff"
                }
            }

            onExited: code => {
                if (code !== 0) {
                    clockPanel.clockTextColor = "#ffffff"
                }
            }
        }

        // --- Fonts ---
         FontLoader {
             id: font_anurati
             source: Qt.resolvedUrl("Anurati.otf")
}

         FontLoader {
             id: font_poppins
		         source: Qt.resolvedUrl("Poppins.ttf")
}

        // --- Time ---
 		SystemClock {
 			id: clock
 			precision: SystemClock.Seconds
}

        // --- Content ---
        Column {
            id: container
                x: clockPanel.centerX
                    ? Math.round((clockPanel.width - width) / 2)
                    : Math.max(0, Math.min(clockPanel.customPosX, Math.max(0, clockPanel.width - width)))
                y: clockPanel.centerY
                    ? Math.round((clockPanel.height - height) / 2)
                    : Math.max(0, Math.min(clockPanel.customPosY, Math.max(0, clockPanel.height - height)))
            Behavior on x {
                NumberAnimation {
                    duration: clockPanel.moveAnimMs
                    easing.type: Easing.OutCubic
                }
            }
            Behavior on y {
                NumberAnimation {
                    duration: clockPanel.moveAnimMs
                    easing.type: Easing.OutCubic
                }
            }
            spacing: 4

// ── Days of the week ──────────────────────────
            Item {
                implicitWidth: clock_day.implicitWidth
                implicitHeight: clock_day.implicitHeight
                anchors.horizontalCenter: parent.horizontalCenter

                // shadow
                Text {
                    x: 2; y: 2
                    text: clock_day.text
                    font: clock_day.font
                    color: "#55000000"
                }
                // Main text
                Text {
                    id: clock_day
                    text: Qt.formatDate(clock.date, "dddd").toUpperCase()
                    font.family: font_anurati.name
                    font.pixelSize: clockPanel.dayFontSize
                    color: clockPanel.clockTextColor
                    font.letterSpacing: 10
                }
            }

            // ── Date ────────────────────────────────
            Item {
                implicitWidth: clock_date.implicitWidth
                implicitHeight: clock_date.implicitHeight
                anchors.horizontalCenter: parent.horizontalCenter

                // shadow
                Text {
                    x: 1; y: 1
                    text: clock_date.text
                    font: clock_date.font
                    color: "#55000000"
                }
                // Main text
                Text {
                    id: clock_date
                    text: Qt.formatDate(clock.date, "dd MMM yyyy").toUpperCase()
                    font.family: font_poppins.name
                    font.pixelSize: clockPanel.dateFontSize
                    color: clockPanel.clockTextColor
                }
            }

            // ── Time  ─────────────────────────────────
            Item {
                implicitWidth: clock_time.implicitWidth
                implicitHeight: clock_time.implicitHeight
                anchors.horizontalCenter: parent.horizontalCenter

                // shadow
                Text {
                    x: 1; y: 1
                    text: clock_time.text
                    font: clock_time.font
                    color: "#55000000"
                }
                // Main text
                Text {
                    id: clock_time
                    text: "- " + Qt.formatTime(clock.date, "hh:mm AP") + " -"
                    font.family: font_poppins.name
                    font.pixelSize: clockPanel.timeFontSize
                    color: clockPanel.clockTextColor
                }
            }
        }
    }
